# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix bamm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0#>
prefix bamm-e: <urn:samm:org.eclipse.esmf.samm:entity:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates edges between Characteristic and Entities or Abstract Entities (dataType)
construct {
  ?edge a :Edge .
  ?edge :title "dataType" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  ?characteristic bamm:dataType ?entityOrAbstractEntity .
  filter( ! strstarts( str( ?entityOrAbstractEntity ), str( xsd: ) )
         && ! strstarts( str( ?entityOrAbstractEntity ), str( rdf: ) )
         && ! strstarts( str( ?entityOrAbstractEntity ), str ( bamm-e: ) ) )
  bind( func:getElementName( ?characteristic ) as ?characteristicName )
  bind( strafter( str( ?entityOrAbstractEntity ), "#" ) as ?entityOrAbstractEntityName ) .
  bind( concat( ?characteristicName, "Characteristic" ) as ?characteristicBoxName )
  bind( iri( concat( str( : ), ?characteristicBoxName ) ) as ?from )

  ?entityOrAbstractEntity rdf:type ?entityOrAbstractEntityClass
  bind( str(?entityOrAbstractEntityClass) as ?entityOrAbstractEntityFullClassName ) .
  bind( strafter(?entityOrAbstractEntityFullClassName, "#") as ?entityOrAbstractEntityTypeName ) .
  bind( concat( ?entityOrAbstractEntityName, ?entityOrAbstractEntityTypeName ) as ?entityOrAbstractEntityBoxName )
  bind( iri( concat( str( : ), ?entityOrAbstractEntityBoxName ) ) as ?to )

  bind( iri( concat( str( : ), ?characteristicBoxName, "_To_", ?entityOrAbstractEntityBoxName ) ) as ?edge )
}
